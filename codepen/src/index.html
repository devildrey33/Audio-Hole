<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Audio Hole</title>
    <link rel="stylesheet" href="./style.css">

    <!-- 
        SHADERS
    -->
    
    <!-- Background fragment shader -->
    <script id="BackgroundFragmentShader" type="x-shader/x-fragment">
        uniform sampler2D uTexture1;
        uniform float     uActualTexture;
        uniform float     uTime;
        uniform float     uAlpha;
        varying vec2      vUv; // Coordenadas UV del fragmento
        
        void main() {
            vec4 color1 = texture2D(uTexture1, vUv);
            gl_FragColor = vec4((color1.rgb ), color1.a * clamp(uTime, 0.0, uAlpha));
            
        }    
    </script>

    <!-- Lateral Osciloscope fragment shader -->
    <script id="HMLOsciloscopeFragmentShader" type="x-shader/x-fragment">
        uniform sampler2D uAudioTexture;    // AUdio data values
        uniform float     uAudioStrength;   // Audio strength
        uniform vec4      uAudioValue;      // Audio average value
        uniform float     uTime;            // Current time
        uniform float     uSpeed;           // Spped
        
        varying vec2      vUv; 
       
        
        // https://shader.how/to/draw-a-sine-wave/
        
        #define PI 3.1415
                
        vec4 Line2(vec3 color, float audioValue, float speed, float width) {
            vec2 pos = vUv - 0.125;
        
            float curve = (0.25 * width) * sin((10.25 * pos.x) - (uSpeed * speed * uTime * 10.0));
        
            float lineAShape = smoothstep(1.0 - clamp(distance(curve + (pos.y) + (audioValue * 0.5), 0.5 ) * 1.0, 0.0, 1.0), 1.0, 0.99);
            vec4  lineACol = (1.0 - lineAShape) * vec4(mix(vec4(color, 1.0), vec4(0.0), lineAShape));
            return lineACol;
        }
        
        void main() {
            float audioValue0 = ((texture2D(uAudioTexture, vec2(mod(uTime + vUv.x * .230, 1.0), 0.0)).g ) * uAudioStrength);    
            float audioValue1 = ((texture2D(uAudioTexture, vec2(mod(uTime + vUv.x * .435, 1.0), 0.0)).g ) * uAudioStrength);    
            float audioValue2 = ((texture2D(uAudioTexture, vec2(mod(uTime + vUv.x * .640, 1.0), 0.0)).g ) * uAudioStrength);    
            float audioValue3 = ((texture2D(uAudioTexture, vec2(mod(uTime + vUv.x * .845, 1.0), 0.0)).g ) * uAudioStrength);    
        
            vec4 finalColor = vec4(0.0, 0.0, 0.0, 0.0);
            finalColor += Line2(vec3(0.5, 0.5, 1.0), audioValue0, .230, uAudioValue.r);
            finalColor += Line2(vec3(0.5, 1.0, 0.5), audioValue1, .435, uAudioValue.g);
            finalColor += Line2(vec3(1.0, 0.5, 0.5), audioValue2, .640, uAudioValue.b);
            vec4 tmpCol = Line2(vec3(1.0, 1.0, 1.0), audioValue3, .845, uAudioValue.a);
            if (tmpCol.a > 0.25) finalColor = tmpCol;
        
            // Discard if its transparent
            if (finalColor.a == 0.0) discard;
        
            // set opacity to simulate depth
            finalColor.a *= (0.001 + uAudioValue.g) * 0.75 * vUv.x;
            //finalColor.a = 1.0;
        
            gl_FragColor = finalColor;
        }

    </script>

    
    
    <!-- Lateral bars fragment shader -->
    <script id="HMLBarsFragmentShader" type="x-shader/x-fragment">
        uniform sampler2D uAudioTexture;    // AUdio data values
        uniform sampler2D uAudioTexture2;    // AUdio data values
        uniform float     uAudioStrength;   // Audio strength
        uniform float     uAudioZoom;       // Audio zoom
        uniform float     uAudioValue;      // Audio average value
        
        uniform float     uTime;            // Current time
        uniform float     uSpeed;           // Spped
        
        varying vec2 vUv;
        
        vec3 Bars(vec3 color, float speed, float amplitude) {
            vec2 pos = vec2(-0.5 + mod(vUv.x + uTime * 0.25 * speed, 1.0), -0.5 + vUv.y);    
        
            float audioValue1 = texture2D(uAudioTexture, vec2(abs(pos.x * 2.0) / uAudioZoom, 0.0)).r * uAudioStrength * amplitude * 0.5;
            float audioValue2 = texture2D(uAudioTexture2, vec2(abs(pos.x * 2.0) / uAudioZoom, 0.0)).r * uAudioStrength * amplitude * 0.5;
            float combinedAudioValue = (audioValue1 + audioValue2) * 0.5;
        
            if (abs(pos.y) < combinedAudioValue) {
                return color * uAudioValue * 0.125;
            }
        
            return vec3(0.0);
        }
        
        void main() {
            vec3 color = vec3(0.0);
            color += Bars(vec3(1.0, 0.20, 0.5), uSpeed * 1.6, 1.0);
            color += Bars(vec3(0.5, 1.0, 0.25), uSpeed * 1.3, 0.9);
            color += Bars(vec3(0.5, 0.25, 1.0), uSpeed * 1.0, 0.8);
            color -= Bars(vec3(0.7, 0.7, 0.0), uSpeed * 1.9, .50);
            if (color.r > 0.0 || color.g > 0.0 || color.b > 0.0) {
                gl_FragColor = vec4(color, .8 * (1.0 - vUv.x));        
            }
            else {
                discard;
            }
        }
    </script>


    <!-- Rays fragment shader -->
    <script id="RaysFragmentShader" type="x-shader/x-fragment">
        uniform float     uAudioValue;      // Audio average value
        uniform float     uSize;            // Line size
        uniform vec3      uColor;           // 
        varying vec2      vUv; 
        
        void main() {
            // Get the audio value from texture green channel
            float audioValue = 1.0;
            
            float curSize = uSize - ((1.0 - vUv.x) * uSize);
            // Its inside the line
            if (abs(vUv.y - audioValue) < curSize) {
                gl_FragColor = vec4(uColor, uAudioValue * 1.0);
            }
            // Its outside the line
            else {
                discard;
            }
        }        
    </script>
    
    <!-- Sun fragment shader -->
    <script id="SunFragmentShader" type="x-shader/x-fragment">
        uniform sampler2D uAudioTexture;
        uniform float     uAudioStrengthFreq;
        uniform float     uAudioStrengthSin;
        uniform float     uTime;
        uniform float     uNoiseStrength;
        uniform float     uNoiseSpeed;
        uniform vec3      uColorSin;
        
        varying vec2      vUv; // Coordenadas UV del fragmento
        
        #define PI   3.14159265
        #define TAU  PI * 2.0
        
        //	Classic Perlin 3D Noise 
        //	by Stefan Gustavson
        //
        vec4 permute(vec4 x)       { return mod(((x*34.0)+1.0)*x, 289.0);            }
        vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }
        vec3 fade(vec3 t)          { return t*t*t*(t*(t*6.0-15.0)+10.0);             }
        
        float cnoise(vec3 P){
            vec3 Pi0 = floor(P); // Integer part for indexing
            vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
            Pi0 = mod(Pi0, 289.0);
            Pi1 = mod(Pi1, 289.0);
            vec3 Pf0 = fract(P); // Fractional part for interpolation
            vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
            vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
            vec4 iy = vec4(Pi0.yy, Pi1.yy);
            vec4 iz0 = Pi0.zzzz;
            vec4 iz1 = Pi1.zzzz;
        
            vec4 ixy = permute(permute(ix) + iy);
            vec4 ixy0 = permute(ixy + iz0);
            vec4 ixy1 = permute(ixy + iz1);
        
            vec4 gx0 = ixy0 / 7.0;
            vec4 gy0 = fract(floor(gx0) / 7.0) - 0.5;
            gx0 = fract(gx0);
            vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
            vec4 sz0 = step(gz0, vec4(0.0));
            gx0 -= sz0 * (step(0.0, gx0) - 0.5);
            gy0 -= sz0 * (step(0.0, gy0) - 0.5);
        
            vec4 gx1 = ixy1 / 7.0;
            vec4 gy1 = fract(floor(gx1) / 7.0) - 0.5;
            gx1 = fract(gx1);
            vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
            vec4 sz1 = step(gz1, vec4(0.0));
            gx1 -= sz1 * (step(0.0, gx1) - 0.5);
            gy1 -= sz1 * (step(0.0, gy1) - 0.5);
        
            vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
            vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
            vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
            vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
            vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
            vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
            vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
            vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);
        
            vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
            g000 *= norm0.x;
            g010 *= norm0.y;
            g100 *= norm0.z;
            g110 *= norm0.w;
            vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
            g001 *= norm1.x;
            g011 *= norm1.y;
            g101 *= norm1.z;
            g111 *= norm1.w;
        
            float n000 = dot(g000, Pf0);
            float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
            float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
            float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
            float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
            float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
            float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
            float n111 = dot(g111, Pf1);
        
            vec3 fade_xyz = fade(Pf0);
            vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
            vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
            float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
            return 2.2 * n_xyz;
        }
        
        
        vec3 getColor(float time) {
          float t = fract(time);  // Fracción de tiempo entre 0 y 1
          
          // Red, green y blue varían en ciclos independientes de tiempo
          float red = cos(t * 6.2831);  // Variación sinusoidal en el rango [-1, 1]
          float green = cos(t * 6.2831); // Variación cosinusoidal en el rango [-1, 1]
          float blue = sin(t * 6.2831 * 2.0); // Variación sinusoidal rápida en el rango [-1, 1]
          
          // Normalización y asignación del color resultante
          vec3 color = vec3(red, green, blue) * 0.5 + 0.75;
          
          return color;
        }
        
        // Make a circle with the frequency data
        vec4 circleFreq(vec4 currentColor, vec2 center) {        
            const float radiusFreq = 0.325;
        
            vec2 pos = vec2(0.55, 0.5);
            float dist = length(vUv - pos);
            float rad = atan(vUv.y - pos.y, vUv.x - pos.x);
        
            float normAngle = rad < 0.0 ? (rad + PI) / PI : 1.0 - (1.0 + (rad - PI) / PI);
        
            float audioValue = (texture2D(uAudioTexture, vec2(normAngle, 0.0)).r - 0.5) * .25 * uAudioStrengthFreq;
            // Perlin noise
            float strength = cnoise(vec3(rad * 2.0, dist * uNoiseStrength,  uTime * uNoiseSpeed)) * 0.1;
        
            if (dist - audioValue + strength + 0.005 < radiusFreq) {
                float angle = uTime * .5 * PI;  // Ángulo de rotación en función del tiempo
                
                // Aplicar matriz de rotación al vector de coordenadas UV
                float cosAngle = cos(angle);
                float sinAngle = sin(angle);
                mat2 rotationMatrix = mat2(cosAngle, -sinAngle, sinAngle, cosAngle);
                vec2 rotatedUV = rotationMatrix * vUv;        
                return vec4(abs(sin(uTime * 0.1)), abs(rotatedUV.x), abs(rotatedUV.y), 0.5); //vec4(color, 1.0);
            }
        
            return currentColor;
        }
        
        
        // // Make a circle with the time domain data
        vec4 circleSin(vec4 currentColor, vec2 center) {
            // Bounce the radius a little
            float radiusSin = 0.165 + sin(uTime) * 0.025;
        
            vec2 pos1 = vec2(0.45, 0.5);
            float dist = length(vUv - center);
            float rad = atan(vUv.y - center.y, vUv.x - center.x);
        
            float normAngle = rad < 0.0 ? (rad + PI) / PI : (1.0 + (rad - PI) / PI);    
        
            float audioValue = (texture2D(uAudioTexture, vec2(normAngle, 0.0)).g - 0.5) * .5 * uAudioStrengthSin;
            // Perlin noise
            float strength = 0.0; //cnoise(vec3(rad * TAU * 5.0, dist * 100.0,  uTime + color.b)) * radius * 0.1;
        
            if (dist - audioValue + strength + 0.01 < radiusSin) {
                return vec4(uColorSin, 0.5);//0.5 + (2.0 * dist) - sin(uTime) * 0.125);
            }
            return currentColor;
        }
        
        void main() {
        
            // Center of the plane
            vec2 center = vec2(0.5, 0.5);
            // Base color
            vec4 color = vec4(0.0, 0.0, 0.0, 0.0);
        
            color = circleFreq(color, center);
            color = circleSin(color, center);
        
            if (color.a == 0.0) discard;
        
            color.a = clamp(uTime * 0.1, 0.0, 1.0);
            gl_FragColor = color;
        }    
    </script>

    <!-- Spirals fragment shader -->
    <script id="SpiralsFragmentShader" type="x-shader/x-fragment">
        uniform sampler2D   uAudioTexture;  // song
        uniform sampler2D   uAudioTexture2; // voice
        uniform float       uAudioStrength;
        uniform float       uAudioZoom;
        uniform float       uAudioStrengthSin;
        uniform float       uAudioZoomSin;
        
        uniform float       uFrequency; // 0.1 are 10 lines, 0.01 are 100 lines
        uniform float       uSpeed;
        uniform float       uThickness;
        uniform float       uMirrors;
        
        uniform float       uFrequencySin; // 0.1 are 10 lines, 0.01 are 100 lines
        uniform float       uSpeedSin;
        uniform float       uThicknessSin;
        uniform vec3        uColorSin;
        
        uniform float       uTime;
        
        uniform float       uAudioValue;
        uniform float       uAudioValue2;
        
        varying vec2        vUv; 
        
        
        // https://www.shadertoy.com/view/XljGzV
        // Created by anastadunbar
        vec3 hsl2rgb( in vec3 c ) {
            vec3 rgb = clamp( abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),6.0)-3.0)-1.0, 0.0, 1.0 );
        
            return c.z + c.y * (rgb-0.5)*(1.0-abs(2.0*c.z-1.0));
        }
        
        
        vec4 drawAudio(vec2 pos) {
            /////////////////
            // Osciloscope //
            /////////////////
            float audioXSin = 2.0 * abs(pos.x - 0.5);
            // Get audio osciloscpe value
            float audioValueSin = (((texture2D(uAudioTexture2, vec2(audioXSin / uAudioZoomSin, 0.0)).g - 0.5) * 0.55) * uAudioStrengthSin) * uFrequencySin;
        
            // Oscyloscope spiral
            vec2 nPosSin = vec2(pos.x, pos.y + (pos.x * uFrequencySin) + mod(uTime * uSpeedSin, 1.0) + audioValueSin);
            float pSin = mod(nPosSin.y, uFrequencySin);
        
            // Paint the spiral osciloscope
            if (pSin < (uFrequencySin * uThicknessSin)) {                        
                    return vec4(uColorSin, (1.0 - pos.y) * uAudioValue * 0.75 );
            }
        
        
            //////////////////
            //     Bars     //
            //////////////////
            float mirrorX = mod(pos.x * uMirrors, 1.0);
            // When pos.x is 0.5 reach the max (1), and then when pos.x is 0 or 1 reach the min (0)
            float audioX = 2.0 * abs(mirrorX - 0.5);
            // Get audio bars value
            float audioValue = ((texture2D(uAudioTexture, vec2((audioX / uAudioZoom), 0.0)).r) * uAudioStrength) * uFrequency;
        
            // Bars spiral
            vec2 nPos = vec2(pos.x, pos.y - (pos.x * uFrequency) + mod(uTime * uSpeed, 1.0) + audioValue);
            // pos y of each line
            float p = mod(nPos.y, uFrequency);
        
            if (p < (uFrequency * uThickness)) {        
                return vec4(hsl2rgb(vec3(0.05 + (uTime * -0.05), 1.0 - p, (1.0 - pos.y) * 0.5 )), uAudioValue * ((1.0 - pos.y)));
            }
            
            discard;
        }
        
        vec4 drawAudio2(vec2 pos) {
            const float mirror    = 2.0;
            const float mirrorSin = 1.0;
            float       audioX    = 0.0;
            float       audioXSin = 0.0;
            // adapt audio position to be 0.0 => 1.0 => 0.0
            // So when x is below 0.5 use pos * 2, else invert the position
            float x = mod(pos.x * mirror, 1.0);
            audioX = (x < 0.5) ? 2.0 * x : 2.0 * (1.0 - x);
            // adapt audio position to be 0.0 => 1.0 => 0.0
            // So when x is below 0.5 use pos * 2, else invert the position
            x = mod(pos.x * mirrorSin, 1.0);
            audioXSin = (x < 0.5) ? 2.0 * x : 2.0 * (1.0 - x);
        
            // Get audio bars value
            float audioValue = ((texture2D(uAudioTexture, vec2((audioX / uAudioZoom), 0.0)).r) * uAudioStrength) * uFrequency;
        
            // Bars spiral
            vec2 nPos = vec2(pos.x, pos.y - (pos.x * uFrequency) + mod(uTime * uSpeed, 1.0) + audioValue);
            // pos y of each line
            float p = mod(nPos.y, uFrequency);
        
        
            // Get audio osciloscpe value
            float audioValueSin = (((texture2D(uAudioTexture2, vec2(audioXSin / uAudioZoomSin, 0.0)).g - 0.5) * 0.55) * uAudioStrengthSin) * uFrequencySin;
        
            // Oscyloscope spiral
            vec2 nPosSin = vec2(pos.x, pos.y + (pos.x * uFrequencySin) + mod(uTime * uSpeedSin, 1.0) + audioValueSin);
            float pSin = mod(nPosSin.y, uFrequencySin);
        
            //float nThickness = uThickness + uAudioValue * 0.05;
        
            // Paint the spiral osciloscope
            if (pSin < (uFrequencySin * uThicknessSin)) {        
                return vec4(uColorSin, ((uAudioValue + uAudioValue2) * 0.5) * ((1.0 - pos.y) * .5));
                //return vec4(hsl2rgb(vec3(uTime * 0.331, 1, pos.y * 0.75 )), pSin * 10.0) * 1.5;
            }
            // Paint the spiral
            if (p < (uFrequency * uThickness)) {        
                return vec4(hsl2rgb(vec3(uTime * -0.05, 1.0 - p, (1.0 - pos.y) * 0.5 )), uAudioValue * ((1.0 - pos.y) * 0.5));
            }
            if (p < (uFrequency * uThickness * 2.0)) {        
                return vec4(hsl2rgb(vec3(0.05 + (uTime * -0.05), 1.0 - p, (1.0 - pos.y) * 0.5 )), uAudioValue * ((1.0 - pos.y) * 0.5));
            }
            discard;
        }
        
        
        void main() {
            vec4 color = drawAudio(vUv);
            gl_FragColor = color;
        }
        
        
    </script>




    <!-- Basic vertex shader with vUv varying -->
    <script id="BasicVertexShader" type="x-shader/x-vertex">
        varying vec2 vUv;

        void main() {
        
            // Aplicar las transformaciones de modelo, vista y proyección
            vec4 modelPosition = modelMatrix * vec4(position, 1.0);
            vec4 viewPosition = viewMatrix * modelPosition;
            vec4 projectionPosition = projectionMatrix * viewPosition;
        
            // Asignar la posición transformada a gl_Position
            gl_Position = projectionPosition;
        
            // Pasar las coordenadas de textura sin transformar a la etapa de fragmentos
            vUv = uv;
        }
    </script>
    
    <!-- pnmdrs postprocessing custom mirror mode fragment shader-->
    <script id="MirrorModeFragment" type="x-shader/x-fragment">
        uniform float uTime;                // current song time comes in seconds
        uniform float uStartTime;           // start time in seconds
        uniform float uEndTime;             // end time in seconds (duration is uEndTime - uStartTime)
        
        uniform float uTimeAnimationIn;     // Time in seconds of the starting animation (split)
        uniform float uTimeAnimationOut;    // Time in seconds of the finish animation   (join)
        uniform float uDisplacement;        // displacement from center (0.5, 0.5), by default 0.125
        uniform vec2  uSize;                // width and height of the viewport        
        
        // https://github.com/glslify/glsl-easings/blob/master/elastic-in-out.glsl
        #define HALF_PI 1.5707963267948966
        float elasticInOut(float t) {
          return t < 0.5
            ? 0.5 * sin(+13.0 * HALF_PI * 2.0 * t) * pow(2.0, 10.0 * (2.0 * t - 1.0))
            : 0.5 * sin(-13.0 * HALF_PI * ((2.0 * t - 1.0) + 1.0)) * pow(2.0, -10.0 * (2.0 * t - 1.0)) + 1.0;
        }        
        
        void mainImage(const in vec4 inputColor, const in vec2 uv, out vec4 outputColor) {
            if (uTime > uStartTime && uTime < uEndTime) {
                // begin animation uTimeAnimationIn  from 0 to 1
                float curPosIn  = elasticInOut(clamp(uTime - uStartTime, 0.0, uTimeAnimationIn) * (1.0 / uTimeAnimationIn));
                // end animation uTimeAnimationOut from 0 to 1
                float curPosOut = elasticInOut(clamp(uTime - (uEndTime - uTimeAnimationOut), 0.0, uTimeAnimationOut) * (1.0 / uTimeAnimationOut));
                // Combine animations and multiply * 0.25 of displacement
                float displacement = (curPosIn - curPosOut) * uDisplacement;
                // Determinate the position of mirrors 
                vec2 pos = uv, pos2 = uv;
                float mixPos = uv.y; // mix set for width < height
                // width its greater than height
                if (uSize.x > uSize.y) { 
                    pos.x  = uv.x + displacement;
                    pos2.x = uv.x - displacement;
                    mixPos = uv.x;
                }
                // height its greater than width
                else { 
                    pos.y  = uv.y + displacement;
                    pos2.y = uv.y - displacement;
                }
                // Get colors from inputBuffer
                vec4 color  = texture2D(inputBuffer, pos);
                vec4 color2 = texture2D(inputBuffer, pos2);
                // Set transparency in oposite ways
                color.a  = 1.0 - uv.x;
                color2.a = uv.x;
                // Combine the final color using both mirrors
                outputColor = mix(color, color2, mixPos);
            }
        }
    </script>

    <!-- pnmdrs postprocessing custom color correction fragment shader -->
    <script id="ColorCorrectionFragment" type="x-shader/x-fragment">
        uniform vec3 powRGB;
        uniform vec3 mulRGB;
        uniform vec3 addRGB;

        void mainImage(const in vec4 inputColor, const in vec2 uv, out vec4 outputColor) {
            outputColor.rgb = mulRGB * pow( ( inputColor.rgb + addRGB ), powRGB );
        }
    </script>

    

</head>
<body>
    <script type="module" src="./script.js"></script>
</body>

</html>